#!/usr/bin/env python3

import os
from scripts.utils import get_out_dir

SCRIPT_DIR = 'scripts'

RUNS = [f'{i+1:04d}' for i in range(config['cellcoal']['no_rep'])]
SUBSAMPLES = config['sc_from_bulk']['subsampling']
REPS = range(config['sc_from_bulk']['no_rep'])

TREES = [i for i,j in config['tree'].items() if j['run']]
CELL_NO = config['cellcoal']['model']['no_cells']
BULK = True

W_MAXS = config.get('poisson', {}).get('w_max', [1000])
if isinstance(W_MAXS, (int, float)):
    W_MAXS = [W_MAXS]

out_dir, filter_dir = get_out_dir(config, BULK)


if BULK:
    if config.get('sc_from_bulk', {}).get('errors', True):
        base_dir = os.path.join(out_dir,
            f'ADO{config["cellcoal"]["scWGA"]["ADO_rate"][0]},' \
            f'{config["cellcoal"]["scWGA"]["ADO_rate"][1]}-{filter_dir}')
    else:
        base_dir = os.path.join(out_dir, f'ADO0-{filter_dir}')
else:
    base_dir = os.path.join(out_dir, filter_dir)

def get_final_files(wildcards):
    files = []
    for tree in TREES:
        if config.get('poisson', {}).get('tree', True) or \
                config.get('poisson', {}).get('run', False):
            files.append(os.path.join(base_dir, f'poissonTree_{tree}',
                    f'poissonTree.summary.tsv'))

    # if config.get('poisson', {}).get('dispersion', False) or \
    #             config.get('poisson', {}).get('run', False):
    #     files.append(os.path.join(base_dir, 'poissonDisp.summary.tsv'))

    return files


rule all:
    input:
        os.path.join(out_dir, 'bulk_summary.tsv'),
        os.path.join(base_dir, 'subsampling_summary.tsv'),


rule merge_bulk_summary_files:
    input:
        get_final_files
    output:
        os.path.join(base_dir, 'subsampling_summary.tsv')
    resources:
        mem_mb = 4096,
        runtime = 20,
    script:
        f'{SCRIPT_DIR}/merge_summaries.py'



# ------------------------------- CELLCOAL -------------------------------------

rule create_cellcoal_config:
    output:
        cellcoal = 'cellcoal_parameters',
        snakemake ='snakemake_config.yaml',
    params:
        template = config['cellcoal'] \
            .get('parameter', 'cellcoal_parameters.template'),
        out_dir = out_dir,
    resources:
        mem_mb = 1024
    script:
        f'{SCRIPT_DIR}/get_cellcoal_config.py'


rule run_cellcoal:
    input:
        cellcoal = 'cellcoal_parameters',
        snakemake ='snakemake_config.yaml',
    output:
        expand(os.path.join(out_dir, 'vcf_dir', 'vcf.{run}'), run=RUNS),
        expand(os.path.join(out_dir, 'trees_dir', 'trees.{run}'), run=RUNS),
    resources:
        mem_mb = 16384 \
            * min(5, (1 + config['cellcoal']['no_rep'] // 1000) * (BULK + 1)),
        runtime = 1200 if CELL_NO >= 100 else 300,
    params:
        exe = config['cellcoal']['exe'],
    shell:
        'mkdir -p {out_dir} && '
        '{params.exe} -F{input} > cellcoal.out 2>&1 && '
        'mv cellcoal.out {out_dir}/cellcoal.out && '
        'mv {input.cellcoal} {out_dir}/config_cellcoal && '
        'mv {input.snakemake} {out_dir}/config_snakemake'


# --------------------------------- BULK ---------------------------------------


rule VCF_to_VAF:
    input:
        vcf = os.path.join(out_dir, 'vcf_dir', 'vcf.{run}')
    output:
        vaf = os.path.join(out_dir, 'vaf_dir', 'vaf.{run}')
    group: 'bulk_analysis'
    script:
        f'{SCRIPT_DIR}/VCF_to_VAF.py'


rule run_mobster:
    input:
        os.path.join(out_dir, 'vaf_dir', 'vaf.{run}')
    output:
        os.path.join(out_dir, 'vaf_dir', 'vaf.{run}.mobster')
    threads: 2
    resources:
        mem_mb = 24576,
        runtime = 120,
    conda: 'r4'
    group: 'bulk_analysis'
    params:
        depth = CELL_NO * config['cellcoal']['NGS']['seq_cov'],
    shell:
        '{SCRIPT_DIR}/run_williams2016.R {input} {output} --depth {params.depth}'


rule merge_mobster:
    input:
        expand(os.path.join(out_dir, 'vaf_dir', 'vaf.{run}.mobster'), \
            run=RUNS)
    output:
        os.path.join(out_dir, 'bulk_summary.tsv')
    script:
        f'{SCRIPT_DIR}/merge_bulk_summaries.py'


# ------------------------------ SUBSAMPLING -----------------------------------


rule create_cellcoal_bulk_config:
    input:
        tree = os.path.join(out_dir, 'trees_dir', 'trees.{run}')
    output:
        cellcoal = temp('cellcoal_parameters_bulk.{run}')
    params:
        template = config['cellcoal'] \
            .get('parameter', 'cellcoal_parameters.template'),
        out_dir = out_dir,
    resources:
        mem_mb = 1024
    group: 'sc_to_bulk'
    script:
        f'{SCRIPT_DIR}/get_cellcoal_config.py'


rule run_cellcoal_from_bulk:
    input:
        'cellcoal_parameters_bulk.{run}'
    output:
        vcf = os.path.join(base_dir, 'vcf_dir', 'vcf.{run}'),
    resources:
        mem_mb = 16384 \
            * min(5, (1 + config['cellcoal']['no_rep'] // 1000) * (BULK + 1)),
        runtime = 1200 if CELL_NO >= 10e0 else 300,
    group: 'sc_to_bulk'
    params:
        exe = config['cellcoal']['exe'],
    shell:
        '{params.exe} -F{input} > /dev/null 2>&1 && '
        'mv {out_dir}.{wildcards.run}/vcf_dir/vcf.0001 {output.vcf} && '
        'rm -r {out_dir}.{wildcards.run}'


# -------------------------- DATA POSTPROCESSING -------------------------------


rule generate_overview_file:
    input:
        os.path.join(base_dir, 'vcf_dir', 'vcf.0001')
    output:
        stats = os.path.join(base_dir, 'data_overview.tsv'),
        ADO = os.path.join(base_dir, 'ADO_overview.tsv')
    resources:
        runtime = 30
    run:
        cols = ['run', 'muts_cells', 'muts_outgrp', 'muts_false',
            'sides', 'TP_sides', 'FP_sides', 'TN_sides', 'FN_sides', 'MS_sides']
        with open(output.stats, 'w') as f:
            f.write('\t'.join(cols) + '\n')

        cells = '\t'.join([f'tumcell{i+1:0>4}' for i in range(CELL_NO)] \
            + ['healthycell'])
        with open(output.ADO, 'w') as f:
            f.write('run\t' + cells + '\n')


rule postprocess_vcf:
    input:
        vcf = os.path.join(base_dir, 'vcf_dir', 'vcf.{run}'),
        stats = ancient(os.path.join(base_dir, 'data_overview.tsv')),
        ADO = ancient(os.path.join(base_dir, 'ADO_overview.tsv'))
    output:
        os.path.join(base_dir, 'vcf_dir', 'vcf.{run}.final.gz')
    resources:
        runtime = 120,
    group: 'sc_to_bulk'
    params:
        dp = config.get('SNP_filter', {}).get('depth', 1),
        gq = config.get('SNP_filter', {}).get('quality', 0),
        s_dp = config.get('SNP_filter', {}).get('singleton_depth', None),
        s_alt = config.get('SNP_filter', {}).get('singleton_alts', None),
        singletons = config.get('SNP_filter', {}).get('singletons', False),
    script:
        f'{SCRIPT_DIR}/postprocess_vcf.py'


rule subsample_vcf:
    input:
        vcf = os.path.join(base_dir, 'vcf_dir', 'vcf.{run}.final.gz'),
        stats = ancient(os.path.join(base_dir, 'data_overview.tsv')),
        ADO = ancient(os.path.join(base_dir, 'ADO_overview.tsv'))
    output:
        expand(os.path.join(base_dir, 'vcf_dir',
            'vcf.{{run}}.ss{{sample}}.{rep}.gz'), rep=REPS)
    params:
        reps = config['sc_from_bulk']['no_rep'],
    script:
        f'{SCRIPT_DIR}/subsample_bulk.py'


def get_repl_data(wildcards):
    vcf = os.path.join(base_dir, 'vcf_dir',
        'vcf.{wildcards.run}.ss{wildcards.sample}.{wildcards.rep}.gz' \
            .format(wildcards=wildcards))

    if wildcards.tree == 'cellphy':
        tree = os.path.join(base_dir, 'cellphy_dir',
            'cellphy_tree.{wildcards.run}.ss{wildcards.sample}.{wildcards.rep}.raxml.bestTree' \
                .format(wildcards=wildcards))
    elif wildcards.tree == 'scite':
        tree = os.path.join(base_dir, 'scite_dir',
            'scite_tree.{wildcards.run}.ss{wildcards.sample}.{wildcards.rep}_ml0.newick'.format(wildcards=wildcards))
    else:
        tree = os.path.join(out_dir, 'trees_dir', 'trees.{wildcards.run}' \
            .format(wildcards=wildcards))

    return {'tree': tree, 'vcf': vcf}

# -------------------------------- SCITE ---------------------------------------

rule run_scite:
    input:
        os.path.join(base_dir, 'vcf_dir', 'vcf.{run}.ss{sample}.{rep}.gz')
    output:
        os.path.join(base_dir, 'scite_dir',
            'scite_tree.{run}.ss{sample}.{rep}_ml0.newick')
    resources:
        mem_mb = 6144,
        runtime = lambda wildcards, attempt: attempt * 1080,
    params:
        steps = config['tree']['scite'].get('steps', 1E6),
        exe = config['tree']['scite'].get('exe', './infSCITE'),
    script:
        f'{SCRIPT_DIR}/run_scite.py'


# ------------------------------- CELLPHY --------------------------------------

rule run_cellphy:
    input:
        os.path.join(base_dir, 'vcf_dir', 'vcf.{run}.ss{sample}.{rep}.gz')
    output:
        os.path.join(base_dir, 'cellphy_dir',
            'cellphy_tree.{run}.ss{sample}.{rep}.raxml.bestTree')
    resources:
        mem_mb = 2048,
        runtime =  lambda wildcards, attempt: attempt * 120,
    threads: 1 if config['tree']['cellphy'].get('command', 'SEARCH') == 'FAST' \
        else 4
    params:
        prefix = lambda wildcards: os.sep.join([base_dir, 'cellphy_dir',
            'cellphy_tree.{wildcards.run}.ss{wildcards.sample}.{wildcards.rep}'\
                .format(wildcards=wildcards)]),
        exe = config['tree']['cellphy']['exe'],
        cmd = config['tree']['cellphy'].get('command', 'SEARCH'),
    shell:
        '{params.exe} {params.cmd} -o healthycell -r -t {threads} -z -l -y '
        '-p {params.prefix} {input} > /dev/null && '
        'rm -f {params.prefix}.raxml.startTree {params.prefix}.raxml.mlTrees '
        '{params.prefix}.raxml.bestModel {params.prefix}.raxml.rba'


# ------------------------------- POISSON --------------------------------------

rule run_poisson:
    input:
        expand(os.path.join(base_dir, 'vcf_dir', 'vcf.{run}.ss{sample}.{rep}.gz'),
            run=RUNS, sample=SUBSAMPLES, rep=REPS)
    output:
        os.path.join(base_dir, 'poissonDisp.summary.tsv')
    resources:
        mem_mb = 2048,
        runtime = 120,
    params:
        exclude = config.get('poisson', {}).get('exclude', ''),
        include = config.get('poisson', {}).get('include', ''),
    script:
        f'{SCRIPT_DIR}/get_poisson_LRT.py'


# --------------------------- POISSON TREE -------------------------------------


def get_errors_bulk(wildcards):
    errors = {'FN': None, 'FP': None}
    if wildcards.tree == 'cellcoal':
        if config.get('sc_from_bulk', {}).get('errors', True):
            errors['FN'] = config['cellcoal']['scWGA']['ADO_rate'][0] / 2
            errors['FP'] = config['cellcoal']['scWGA']['ampl_error'][0] \
                + config['cellcoal']['NGS']['seq_error']
        else:
            errors['FN'] = 0.0
            errors['FP'] = 0.0
    return errors


rule run_poissonTree:
    input:
        unpack(get_repl_data)
    output:
        temp(os.path.join(base_dir, 'poissonTree_{tree}',
            'poissonTree.{run}.ss{sample}.{rep}.LRT.txt'))
    envmodules:
        'ete/3.1.2',
    threads: 1
    resources:
        mem_mb = 768,
        runtime = lambda wildcards, attempt: attempt * 30,
    #group: 'treeTest'
    params:
        exclude = config.get('poisson', {}).get('exclude', ''),
        include = config.get('poisson', {}).get('include', ''),
        w_maxs = W_MAXS,
        errors = get_errors_bulk
    script:
        f'{SCRIPT_DIR}/get_poisson_tree_LRT.py'


rule merge_poissonTree_tree:
    input:
        expand(os.path.join(base_dir, 'poissonTree_{{tree}}',
                'poissonTree.{run}.ss{sample}.{rep}.LRT.txt'),
            run=RUNS, sample=SUBSAMPLES, rep=REPS)
    output:
        os.path.join(base_dir, 'poissonTree_{tree}',
            'poissonTree.summary.tsv')
    resources:
        mem_mb = 2048,
    script:
        f'{SCRIPT_DIR}/merge_poisson_tree_LRT.py'