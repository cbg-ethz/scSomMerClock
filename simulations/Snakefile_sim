#!/usr/bin/env python3

import os
import sys
from utils import *

MODELS = ['clock', 'noClock']
RUNS = [f'{i+1:04d}' for i in range(config['cellcoal']['no_rep'])]

scheduler = config.get('scheduler', None)

if isinstance(config['mrbayes']['ngen'], (int, float)):
    config['mrbayes']['ngen'] = [config['mrbayes']['ngen']]
out_dir = get_out_dir(config)


def get_mrbayes_hpc_runtime(steps):
    steps_int = int(steps)
    if steps_int <= 5E6:
        return '04:00'
    elif steps_int <= 10E6:
        return '08:00'
    elif steps_int <= 20E6:
        return '16:00'
    elif steps_int <= 40E6:
        return '24:00'
    elif steps_int <= 60E6:
        return '32:00'
    elif steps_int <= 80E6:
        return '36:00'
    else:
        return '48:00'


rule all:
    input:
        os.path.join(out_dir, 'clock_test_summary.tsv'),
        expand(os.path.join(out_dir, 'true_haplotypes_dir', 'true_vcf.{run}'),
            run=RUNS)
            

rule create_cellcoal_config:
    output:
        os.path.join('cellcoal_parameters')
    params:
        template = config['cellcoal']['parameter']
    resources:
        mem_mb = 1024
    run:
        cc_config = get_cellcoal_config(config, params.template, out_dir)
        with open(output[0], 'w') as f:
            f.write(cc_config)


rule run_cellcoal:
    input:
        os.path.join('cellcoal_parameters')
    output:
        expand(os.path.join(out_dir, 'vcf_dir', 'vcf.{run}'), run=RUNS),
        expand(os.path.join(out_dir, 'true_haplotypes_dir', 'true_hap.{run}'),
            run=RUNS)

    resources:
        mem_mb = 32768
    params:
        exe = config['cellcoal']['exe']
    shell:
        'mkdir -p {out_dir} && {params.exe} -F{input} && mv {input} {out_dir}/{input}'


rule get_true_GT:
    input:
        os.path.join(out_dir, 'true_haplotypes_dir', 'true_hap.{run}')
    output:
        os.path.join(out_dir, 'true_haplotypes_dir', 'true_vcf.{run}')
    resources:
        mem_mb = 4096
    run:
        haplotypes_to_vcf(input[0], output[0])


rule vcf_to_mpileup:
    input:
        os.path.join(out_dir, 'vcf_dir', 'vcf.{run}')
    output:
        pileup = os.path.join(out_dir, 'vcf_dir', 'vcf.{run}.mpileup'),
        samples = os.path.join(out_dir, 'vcf_dir', 'vcf.{run}.SampleNames.txt')
    resources:
        mem_mb = 4096
    run:
        vcf_to_pileup(input[0], output.pileup, output.samples)


rule run_monovar:
    input:
        pileup = os.path.join(out_dir, 'vcf_dir', 'vcf.{run}.mpileup'),
        samples = os.path.join(out_dir, 'vcf_dir', 'vcf.{run}.SampleNames.txt')
    output:
        os.path.join(out_dir, 'vcf_dir', 'vcf.{run}.monovar.vcf')
    threads: 2
    resources:
        mem_mb = 4096 if scheduler == 'lsf' else 2 * 4096
    conda:
        'envs/monovar.yaml'
    envmodules:
        'scipy/1.4.1-python-3.7.7',
        'pysam/0.16.0.1-python-3.7.7'
    params:
        exe = config.get('monovar', {}).get('exe', '')
    shell:
        'python {params.exe} -i {input.pileup} -s {input.samples} -o {output} '
        '-m {threads}'


if config.get('monovar', {}).get('run', False):
    vcf_in_file = 'vcf.{run}.monovar.vcf'
else:
    vcf_in_file = 'vcf.{run}'

rule vcf_to_nex:
    input:
        os.path.join(out_dir, 'vcf_dir', vcf_in_file)
    output:
        expand(os.path.join(out_dir, 'nxs_dir', 'nxs.{{run}}.{{steps}}.{model}'),
            model=MODELS)
    resources:
        mem_mb = 1024
    params:
        ss = config['mrbayes'].get('ss', False)
    run:
        vcf_to_nex(input[0], output, wildcards.steps, params.ss)


if not scheduler:
    rule run_mrbayes:
        input:
            os.path.join(out_dir, 'nxs_dir', 'nxs.{run}.{steps}.{model}')
        output:
            os.path.join(out_dir, 'nxs_dir', 'nxs.{run}.{steps}.{model}.lstat')
        params:
            mb_exe = config['mrbayes']['exe']
        resources:
            mem_mb = 16384
        shell:
            'cd {out_dir} && '
            '{params.mb_exe} nxs_dir/nxs.{wildcards.run}.{wildcards.steps}.{wildcards.model} '
            '> {input}.log && '
            'mv nxs.{wildcards.run}.{wildcards.steps}.{wildcards.model}.* nxs_dir'
else:
    rule run_mrbayes_hpc:
        input:
            os.path.join(out_dir, 'nxs_dir', 'nxs.{run}.{steps}.{model}')
        output:
            os.path.join(out_dir, 'nxs_dir', 'nxs.{run}.{steps}.{model}.lstat')
        conda:
            'envs/mrbayes.yaml'
        envmodules:
            'gcc/6.4.0',
            'openmpi/2.1.1',
            'mrbayes/3.2.7'
        threads: 4
        resources:
            mem_mb = 4096 if scheduler == 'lsf' else 4 * 4096,
            runtime = lambda wildcards: get_mrbayes_hpc_runtime(wildcards.steps),
        shell:
            'cd {out_dir} && '
            'mpirun mb nxs_dir/nxs.{wildcards.run}.{wildcards.steps}.{wildcards.model} '
            '> nxs.{wildcards.run}.{wildcards.steps}.{wildcards.model}.log && '
            'mv nxs.{wildcards.run}.{wildcards.steps}.{wildcards.model}.* nxs_dir'


rule merge_results:
    input:
        expand(os.path.join(out_dir, 'nxs_dir', 'nxs.{run}.{steps}.{model}.lstat'),
            run=RUNS, steps=config['mrbayes']['ngen'], model=MODELS)
    output:
        os.path.join(out_dir, 'clock_test_summary.tsv')
    resources:
        mem_mb=2048
    run:
        get_Bayes_factor(input, output[0])

